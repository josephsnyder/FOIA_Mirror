<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25" zv="Cache for UNIX (Red Hat Enterprise Linux for x86-64) 2014.1.3 (Build 775)" ts="2017-10-31 10:16:54">
<Class name="SecurityTokenService.RequestSecurityToken">
<Super>%SOAP.WebClient</Super>
<TimeChanged>64576,52908.833897</TimeChanged>
<TimeCreated>64322,32364.932688</TimeCreated>

<Parameter name="ELEMENTQUALIFIED">
<Description>
Non-default elementFormQualified for types XML namespace.</Description>
<Default>0</Default>
</Parameter>

<Parameter name="LOCATION">
<Description>
This is the URL used to access the web service.</Description>
<Default>https://services.aadomain.ext:9301/STS/RequestSecurityToken</Default>
</Parameter>

<Parameter name="NAMESPACE">
<Description>
This is the namespace used by the Service</Description>
<Default>http://docs.oasis-open.org/ws-sx/ws-trust/200512</Default>
</Parameter>

<Parameter name="OUTPUTTYPEATTRIBUTE">
<Description>
Use xsi:type attribute for literal types.</Description>
<Default>1</Default>
</Parameter>

<Parameter name="SECURITYIN">
<Description>
Determines handling of Security header.</Description>
<Default>ALLOW</Default>
</Parameter>

<Parameter name="SERVICENAME">
<Description>
This is the name of the Service</Description>
<Default>SecurityTokenService</Default>
</Parameter>

<Parameter name="SOAPVERSION">
<Description>
This is the SOAP version supported by the service.</Description>
<Default>1.1</Default>
</Parameter>

<Parameter name="SOAPPREFIX">
<Description>
This is the SOAP Prefix</Description>
<Default>soap</Default>
</Parameter>

<Parameter name="RESPONSETYPENAMESPACE">
<Description>
Added to prevent invalid tag error</Description>
<Default>http://schemas.xmlsoap.org/ws/2005/02/trust</Default>
</Parameter>

<Method name="RequestSecurityToken">
<Final>1</Final>
<FormalSpec><![CDATA[&any:%ListOfDataTypes(ELEMENTTYPE="%XML.String",XMLNAME="any",XMLPROJECTION="any")]]></FormalSpec>
<ProcedureBlock>1</ProcedureBlock>
<WebMethod>1</WebMethod>
<SoapBindingStyle>document</SoapBindingStyle>
<SoapBodyUse>literal</SoapBodyUse>
<Implementation><![CDATA[ Do (..WebMethod("RequestSecurityToken")).Invoke($this,"",.any)
]]></Implementation>
</Method>

<Method name="BeginSOAPEnvelope">
<Description>
Override SOAP envelope definitions</Description>
<Internal>1</Internal>
<FormalSpec><![CDATA[bindingStyle:%String,bodyUse:%String,namespace:%String,typeNamespace:%String,forcePrefix:%Boolean,&namespaces:%XML.Namespaces]]></FormalSpec>
<ServerOnly>1</ServerOnly>
<Implementation><![CDATA[
	Set namespaces=##class(%XML.Namespaces).%New()
	If $case(namespace,$$$SOAPWSTNS:1,$$$SOAPWSRMns:1,:0) {
		Set elementQualified=1
		Set attributeQualified=0
		Set namespaces.OutputTypeAttribute=0
	} Else {
		Set elementQualified=..#ELEMENTQUALIFIED
		If elementQualified="" Set elementQualified=(bodyUse="literal")
		Set attributeQualified=..#ATTRIBUTEQUALIFIED
		Set namespaces.OutputTypeAttribute=..#OUTPUTTYPEATTRIBUTE
	}
	Do namespaces.AddInstanceNamespace("xsi")
	Do namespaces.AddSchemaNamespace("s")
	If ..SoapVersion="1.2" {
		Set soapns=$$$SOAP12ENVns
	} Else {
		Set soapns=$$$SOAPENVns
	}
	Do namespaces.AddNamespace(soapns,..#SOAPPREFIX)
	If ..RequestMessageStart["BOM" {
		If $$$XMLIsWrite $$$SETIO("RAW")
		$$$XMLSetBuffer($char(239,187,191))
		$$$XMLWriteRAW
		If $$$XMLIsWrite $$$SETIO("UTF8")
	}
	If ..RequestMessageStart["DCL" $$$XMLSetBuffer("<?xml version=""1.0"" encoding=""UTF-8"" ?>") $$$XMLWriteLine
	$$$XMLSetBuffer("<"_..#SOAPPREFIX_":Envelope xmlns:"_..#SOAPPREFIX_"="""_soapns_"""")
	$$$XMLAppendBuffer(" xmlns:xsi="""_$$$XSIns_"""")
	$$$XMLAppendBuffer(" xmlns:s="""_$$$XSDns_"""")
	$$$XMLAppendBuffer(" xmlns:wsa=""http://schemas.xmlsoap.org/ws/2004/08/addressing""")
	Do namespaces.AddNamespace("http://schemas.xmlsoap.org/ws/2004/08/addressing","wsa")
	$$$XMLAppendBuffer(" xmlns:wss=""http://docs.oasis-open.org/wss/2004/01/oasis-200401-wss-wssecurity-secext-1.0.xsd""")
	Do namespaces.AddNamespace("http://docs.oasis-open.org/wss/2004/01/oasis-200401-wss-wssecurity-secext-1.0.xsd","wss")
	$$$XMLAppendBuffer(" xmlns:wsp=""http://schemas.xmlsoap.org/ws/2004/09/policy""")
	Do namespaces.AddNamespace("http://schemas.xmlsoap.org/ws/2004/09/policy","wsp")
	$$$XMLAppendBuffer(" xmlns:ns1=""http://docs.oasis-open.org/ws-sx/ws-trust/200512""")
	Do namespaces.AddNamespace("http://schemas.xmlsoap.org/ws/2004/09/policy","ns1")
		
	If bodyUse="encoded" {
		Set soapenc=$select(..SoapVersion="1.2":$$$SOAP12ENCns,1:$$$SOAPENCns)
		$$$XMLAppendBuffer(" xmlns:SOAP-ENC='"_soapenc_"'")
		Do namespaces.AddNamespace(soapenc,"SOAP-ENC")
	}
	If (bindingStyle="rpc") && (namespace'=typeNamespace) Set forcePrefix=1
	If forcePrefix || (bindingStyle="rpc") {
		$$$XMLAppendBuffer(" xmlns:tns="""_namespace_"""")
		Do namespaces.AddNamespace(namespace,"tns")
	}
	If forcePrefix || (bodyUse="encoded") || 
	   ((bindingStyle="document")&&('elementQualified||attributeQualified)&&(typeNamespace'="")) {
		$$$XMLAppendBuffer(" xmlns:types='"_typeNamespace_"'")
		Do namespaces.AddNamespace(typeNamespace,"types")
	}
	If ..AddressingOut'="" {
		Set addrNamespace=..AddressingOut.Namespace()
		$$$XMLAppendBuffer(" xmlns:wsa='"_addrNamespace_"'")
		Do namespaces.AddNamespace(addrNamespace,"wsa")
	}
	If $isobject(r%SecurityOut) && (r%SecurityOut.SecurityElement.Count()>0) {
		$$$XMLAppendBuffer(" xmlns:wsse='"_$$$SOAPWSSEns_"'")
		Do namespaces.AddNamespace($$$SOAPWSSEns,"wsse")
		$$$XMLAppendBuffer(" xmlns:wsu='"_$$$SOAPWSUns_"'")
		Do namespaces.AddNamespace($$$SOAPWSUns,"wsu")
	} ElseIf namespace=$$$SOAPWSTNS {
		$$$XMLAppendBuffer(" xmlns:wsu="""_$$$SOAPWSUns_"""")
		Do namespaces.AddNamespace($$$SOAPWSUns,"wsu")
	}
	$$$XMLAppendBuffer(">")
	$$$XMLWriteLine
	Do namespaces.DefineAllNamespacePrefixes()
	Set namespaces.CurrentNamespace=""
	Set namespaces.DefaultNamespace=typeNamespace
	Set namespaces.ElementQualified=elementQualified
	Quit $$$OK
]]></Implementation>
</Method>
</Class>


<CSP name="dgbtClaims.csp" application="/csp/bt/"><![CDATA[
<!DOCTYPE html "-//W3C//DTD XHTML 1.0 Strict//EN" 
  "http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd">
<html xmlns="http://www.w3.org/1999/xhtml">
<head>	
    <meta http-equiv="content-type" content="text/html; charset=utf-8"/>
    <title>Beneficiary Travel Dashboard 1.0 p32</title>
    
    <script src="jquery-ui-1.8.4.custom/js/jquery-1.4.2.min.js" type="text/javascript"></script>
    <script src="jquery-ui-1.8.4.custom/js/jquery-ui-1.8.4.custom.min.js" type="text/javascript"></script>
    <script src="DataTables-1.8.0/media/js/jquery.dataTables.js" language="JavaScript"></script>
    <script src="num-html.js" language="JavaScript"></script>
		
	<link type="text/css" rel="stylesheet" href="bt.css" />
	<link href="jquery-ui-1.8.4.custom/css/smoothness/jquery-ui-1.8.4.custom.css" rel="Stylesheet" type="text/css" />
   	<link href="DataTables-1.8.0/media/css/demo_table.css" rel="stylesheet" type="text/css">	    
   	  
   	<script type="text/javascript">	
	$(document).ready(function() {
		#server(dgbt.DashboardCb.OnLoad())#;
		
		oTable = $('#myTable').dataTable({
			"bFilter": true,
			"bPaginate": false,
			"sPaginationType": "two_button",
			"bProcessing": true,
			"bAutoWidth": false,
			"aaSorting": [[2,'desc']],
			"aoColumns": [
				{ "sTitle": "<a style=\"color:black;\" href=\"#\">Patient</a>" },
				{ "sTitle": "<a style=\"color:black;\" href=\"#\">Amount Payable</a>", "sType":"num-html" },
				//{ "sTitle": "<a style=\"color:black;\" href=\"#\">Status</a>" },
				//{ "sTitle": "<a style=\"color:black;\" href=\"#\">Status Changed By</a>" },
				//{ "sTitle": "<a style=\"color:black;\" href=\"#\">Status Date/Time</a>" },
				{ "sTitle": "<a style=\"color:black;\" href=\"#\">Claim Date</a>", "sType":"date" },
				{ "sTitle": "<a style=\"color:black;\" href=\"#\">Created By</a>" }
	   		]
		});
		
		// hacks for 508 compliance		
		//$('.dataTables_filter').html("<label>Search:<input type=\"text\"></label>");
		$('.dataTables_filter').prepend("<label>");
		$('.dataTables_filter').append("</label>");		
		//$('#myTable_previous').wrap('<a href="#" title="previous" />');
		//$('#myTable_next').wrap('<a href="#" title="next" />');
		
		#call(..DisplayClaims())#;
		setInterval("RefreshView()",300000);		
	});
	
	function RefreshView() {
		#call(..DisplayClaims())#;
	}
	
	function MarkPaid(ien) {
		#server(..MarkPaid(ien))#;
	}
	</script>
   		
 	<script language="cache" runat="server">
	S %session.Data("BT_CLAIMIEN")=0
	D ##class(dgbt.Audit).LogAction(3)	
	</script>
</head>
<body>
	<csp:include page="dgbtMenu.csp" />
	
	<div>
		<table style="margin-bottom:10px;">
			<tr>
				<td>Claims:</td>
				<td><input id="numclaims" readonly="true" /></td>
			</tr>
			<tr>
				<td>Amount Payable:</td>
				<td><input id="amountpayable" readonly="true" /></td>
			</tr>
		</table>
		
		<table id="myTable" class="display"></table>
	</div>
	  	
	<script language="cache" method="DisplayClaims">
	S CLAIMIEN=9999999999 
	S CLAIMIEN=$O(^DGBT(392,"B",CLAIMIEN),-1)
	Q:CLAIMIEN=$G(%session.Data("BT_CLAIMIEN"))
	
	&js< var array1 = []; >
	
	S COUNT=0,TOTAL=0
 	S CLAIMDATE=$$FMADD^XLFDT($$DT^XLFDT,-1,0,0,0)_".999999"
 	S ENDDATE=$$DT^XLFDT_".999999"
 	S LOOPDT=CLAIMDATE 
 	F  S LOOPDT=$O(^DGBT(392,"B",LOOPDT)) Q:((LOOPDT<1)!(LOOPDT>ENDDATE))  D
 	.S CLAIMIEN="" F  S CLAIMIEN=$O(^DGBT(392,"B",LOOPDT,CLAIMIEN)) Q:(CLAIMIEN="")  D
 	..K ARRAY D GETS^DIQ(392,CLAIMIEN,".01;2;10;12","EI","ARRAY","")
 	..S AMOUNTPAYABLE=$G(ARRAY(392,CLAIMIEN_",",10,"E"))
 	..S PATIENT=$G(ARRAY(392,CLAIMIEN_",",2,"E"))
 	..S AJRDFN=$G(ARRAY(392,CLAIMIEN_",",2,"I"))
 	..S LAST4=$E($$GET1^DIQ(2,AJRDFN,.09),6,9)
 	..S CLAIMDATE=$G(ARRAY(392,CLAIMIEN_",",.01,"I")),CLAIMDATE=$$FMTE^XLFDT(CLAIMDATE,"5"),CLAIMDATE=$TR(CLAIMDATE,"@"," ")
 	..S CREATEDBY=$G(ARRAY(392,CLAIMIEN_",",12,"E"))
 	..&js<
		array1.push([
			"#(PATIENT)# (#(LAST4)#)",
			"#(AMOUNTPAYABLE)#",
			"#(CLAIMDATE)#",
	  		"#(CREATEDBY)#"
  		]);  		
  		>
  	..S COUNT=COUNT+1
  	..S TOTAL=TOTAL+AMOUNTPAYABLE
  	..S %session.Data("BT_CLAIMIEN")=CLAIMIEN
  		
  	&js<
  		oTable.fnClearTable(0);
	 	if (array1.length != 0) {
			oTable.fnAddData(array1);
	 	}
	 	$("#numclaims").val("#(COUNT)#");
	 	$("#amountpayable").val("#(TOTAL)#");
 		>
	</script>
</body>
</html>
]]></CSP>


<CSP name="dgbtDashboard.csp" application="/csp/bt/"><![CDATA[
<!DOCTYPE html "-//W3C//DTD XHTML 1.0 Strict//EN" 
  "http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd">
<html xmlns="http://www.w3.org/1999/xhtml">
<head>	
    <title>Beneficiary Travel Dashboard 1.0 p32</title>
	   
    <script src="jquery-ui-1.8.4.custom/js/jquery-1.4.2.min.js" type="text/javascript"></script>
    <script src="jquery-ui-1.8.4.custom/js/jquery-ui-1.8.4.custom.min.js" type="text/javascript"></script>
    <script src="DataTables-1.8.0/media/js/jquery.dataTables.js" language="JavaScript"></script>
	<script type="text/javascript" src="jquery.cluetip.js"></script>
	<script type="text/javascript" src="num-html.js"></script>
	
	<link type="text/css" rel="stylesheet" href="bt.css" />
   	<link type="text/css" rel="stylesheet" href="jquery.cluetip.css" />
   	<link href="jquery-ui-1.8.4.custom/css/smoothness/jquery-ui-1.8.4.custom.css" rel="Stylesheet" type="text/css" />
   	<link href="DataTables-1.8.0/media/css/demo_table.css" rel="stylesheet" type="text/css">	      	  
   	   
   	<script type="text/javascript">	
   	function Site() {
	   	this.StationNumber = "";
		this.Name = "";
		this.Address = "";
		this.SpecialtyList = "";
		this.Counter = 0;
	}    	
   	
    var apiKey = "AjCpaLGr0GKK-J8bsskxiMX6CbAKJ8Hm2JynzaAPdgzw4iKqO73nTTV182BvfJSr";
   	var sitenum = 0
    var siteArray = [];
    var patientAddress = "";
    var patientZip = "";
    var patientBing = "";
    var timer = null;
    	
	$(document).ready(function() {
		initialize();
		
		oTable = $('#myTable').dataTable({
			"bFilter": false,
			"bPaginate": false,
			"bAutoWidth": false,
			"aaSorting": [[1,'asc']],
			"aoColumns": [
				{ "sTitle": "<a style=\"color:black;\" href=\"#\">Site</a>", "sWidth": "100px", "bSortable":false },
				{ "sTitle": "<a style=\"color:black;\" href=\"#\">Mileage</a>", "sWidth": "50px", "sType":"num-html" }
	   		]
		});
	});
   
    function initialize() {	
    	#server(dgbt.DashboardCb.OnLoad())#;
    	#server(dgbt.Institution.InitializeInstitutions())#;
    	#server(..AfterLoad())#;
    	setInterval('Refresh()',3500);
    }
       		
	function Refresh() {
	    try {		    
		    #call(..RefreshView(document.getElementById('claimer').value,document.getElementById('ajrdfn').value))#;
	    } catch (err) {
	    }
    }
    
    function CalculateMileage() {  
	  	var url = "https://dev.virtualearth.net/REST/V1/Routes/Driving"
	  		+ "?output=json"
	  		+ "&jsonp=PatientAddressCallback" 
			+ "&wp.0=" + patientAddress 
			+ "&wp.1=" + patientZip
			+ "&optmz=" + $("#optimize").val()
			+ "&distanceUnit=Mile"
			+ "&key=" + apiKey;

		 $.ajax({
  			url: url
		    , dataType : "jsonp"
  		});
	}
	 
	function PatientAddressCallback(json) { 		
		if (json.statusCode == 200) {
			patientBing = patientAddress
			MapAllSites();
		} else {
			$("#paddress").val(patientZip);
			patientBing = patientZip;
			UseAlternate();
		}
    }

    function getURL(i) {	    	    	    
	    return  "https://dev.virtualearth.net/REST/V1/Routes/Driving?"
			+ "output=json"
			+ "&jsonp=BingCallback"
	    	+ "&jsonso=" + i + "-" + timer.getMilliseconds()
	    	+ "&wp.0=" + patientBing
	    	+ "&wp.1=" + siteArray[i].Address 
	    	+ "&optmz=" + $("#optimize").val()
	    	+ "&distanceUnit=Mile"
	    	+ "&key=" + apiKey;
    }

    function CallBingService(url) { 
	    $.ajax({
  			url: url
		    , dataType : "jsonp"
  		});
    }
    
    function BingCallback(json,siteSID) {
	    var siteBinged = siteSID.split("-")
	    siteIndex = siteBinged[0];
	    siteTimer = siteBinged[1];
	          	
      	if (siteTimer != timer.getMilliseconds()) {
	      	return;
      	}
	    	
      	var distance = 0;

      	try {     	
	    	if (json.statusCode == 200) {
	      		if (json.resourceSets.length > 0) {
					distance = json.resourceSets[0].resources[0].travelDistance.toFixed(1);
	    		}
	    	} else if (json.statusCode == 500 || json.StatusCode == 503) {
	    		var url = getURL(siteIndex);
	    		CallBingService(url);
	    		return;
    	  	} else if (json.statusCode == 404) {
		      	if (siteArray[siteIndex].Counter < 2) {
		    		siteArray[siteIndex].Counter++;
		    		var url = getURL(siteIndex);
	    	  		CallBingService(url);
	    	  		return;
		      	} else {
		      		distance = "Check Address";
	    		}
			}
      	} catch(err) {
      		if (siteArray[siteIndex].Counter == 0) {
		    	siteArray[siteIndex].Counter++;
		    	var url = getURL(siteIndex);
	    	  	CallBingService(url);
	    	  	return;
		    } else {
		    	distance = "Check Address";
	    	}
	   	}
      	      	
      	var optimize = 0;
      	if ($("#optimize").val() == 'distance') {
	      	optimize = 1
      	}
      	
      	var site = new Site();
      	site = siteArray[siteIndex];
      	var alttext = site.SpecialtyList.replace(/<br \/>/g, ",");      	
		var url = '<a target="_BLANK" href="http://bing.com/maps/default.aspx?v=2&rtop=' + optimize + '~0~0&rtp=adr.' + patientBing + '~adr.' + site.Address + '">' + distance + '</a>';
		var siteName='<a ref="#" style="color:black;" id="site' + siteIndex + '" class="' + site.Name + "|" + site.Address + '<br/><br/>' + site.SpecialtyList + '" href="#" title="' + site.Name + '">' + site.Name + '</a>';
		oTable.fnAddData( [siteName,url], true);				
	    
	    $('#site' + siteIndex).cluetip({ activation: 'click', sticky: 'true', closePosition: 'title', splitTitle: "|", titleAttribute: 'class' });
		$('#site' + siteIndex).click(function(){
			$('#cluetip-inner').attr("title",alttext);
			$('#cluetip-close a').focus();				
		});	    
    }
        
    function MapAllSites() { 
    	timer = new Date(); 
	  	oTable.fnClearTable(0);	  	
	   		  		  	
	  	for (var i = 0; i < siteArray.length; i++) {
		  	siteArray[i].Counter = 0;
	      	var url = getURL(i);
		    CallBingService(url);		    
	 	}
	}        	
  	   	
	function UseAlternate() {		
		if ($("#paddress").val() != "") {
			patientBing = $("#paddress").val();

			MapAllSites();
			
		} else {
			MapAllSites();
		}
	}
	</script>
</head>
<body>
	<csp:include page="dgbtMenu.csp" />
	
	<div id="claimerdiv">
		<label for="claimer">Claimer:<label>
		<select id="claimer" onchange="#call(..RefreshView(this.value,document.getElementById('ajrdfn').value))#;">
		</select>
	</div>
	
	<div style="margin-top:5px;">
		<label for="optimize">Optimize:<label>
		<select id="optimize" onchange="UseAlternate()">
			<option value="time">Time</option>
			<option value="distance">Distance</option>
		</select>
	</div>

	<div id="wrapper">
		<div id="left">			 	
			<div id="content"></div>
			<input type="hidden" id="ajrdfn" />
		</div>
	
		<div id="right">
			<table id="myTable" class="display"></table>
		</div>
	</div>
	  
  	<script language="cache" method="AfterLoad">
  	D ##class(dgbt.DashboardCb).InitializeClaimers()
  	I $G(%session.Data("CLAIMER"))'="" D
  	.S AJRDFN=$G(^XTMP("DGBT BTD","CLAIMERS",$G(%session.Data("CLAIMER"))))
	</script>
		
	<script language="cache" method="RefreshView" arguments="AJRDUZ:%String,LASTAJRDFN:%String">
	Q:$G(AJRDUZ)<1
	S AJRDFN=$G(^XTMP("DGBT BTD","CLAIMERS",AJRDUZ))
	I ((AJRDFN>0)&(AJRDFN'=$G(LASTAJRDFN))) D ##class(dgbt.DashboardCb).UpdateDisplay(AJRDFN)
	S %session.Data("CLAIMER")=AJRDUZ
	</script>
</body>
</html>
]]></CSP>


<CSP name="dgbtLogin.csp" application="/csp/bt/"><![CDATA[
<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01//EN" "http://www.w3.org/TR/html4/strict.dtd">
<html lang="en">

<head>
	<title>Beneficiary Travel Dashboard 1.0 p32</title>
	
    <style type="text/CSS">
    body { background-color:#e8e8e8; font-family:sans-serif; font-size:small; color:#00008b; }
	div.pageheader { font-weight:bold; position:fixed; top:0px; left:0px;  border-bottom:solid 1px black; background-color:#1e2d54;
  					width:98.5%; height:1.5em; color:White; font-size:12px;}
	div.pagebody { position:fixed; top:1.5em; left:0px; bottom:0px; width:99.9%; background-color:#e8e8e8; }
	div.center { margin-right:auto; margin-left:auto; text-align:center; }
	h1.error { color:red; }
   </style>   
</head>
<body onLoad="document.getElementById('submit').focus();">
	
	<script language="cache" runat="server">

   I $G(%request.CgiEnvs("HTTP_REFERER"))[".domain.ext/"
   {
    S %session.Data("LogoutURL")=$G(%request.CgiEnvs("HTTP_REFERER"),"NOURL")
   }
   S MESSAGE="0^System Use Notification"
   K %session.Data("AUTHFAIL"),%session.Data("WebError")
	
   //if user clicks continue button, process SAML token (login)
   I ($Data(%request.Data("submit",1))) 
   {
	 S IAM = %request.GetCookie("IAMSESSION") //IAM cookie used to authenticate
 	 S CLIENT = ##class(SecurityTokenService.RequestSecurityToken).%New()
 	 S CLIENT.SSLConfiguration = "dashboardssoi" //cache ssl/tls configuration
 	
  	 //Build the SOAP request
 	 S SMSG = ##class(%ListOfDataTypes).%New()
 	 D SMSG.Insert("<ns1:OnBehalfOf>")
 	 D SMSG.Insert("<ns1:Base>")
 	 D SMSG.Insert("<wss:BinarySecurityToken EncodingType=""base64"" ValueType=""http://ssoi.domain.ext/siteminder/std_token"">"_IAM_"</wss:BinarySecurityToken>")
 	 D SMSG.Insert("</ns1:Base>")
 	 D SMSG.Insert("</ns1:OnBehalfOf>")
 	 D SMSG.Insert("<wsp:AppliesTo>")
 	 D SMSG.Insert("<wsa:EndpointReference>")
 	 D SMSG.Insert("<wsa:Address>https://ppd.ehmp.domain</wsa:Address>")
 	 D SMSG.Insert("</wsa:EndpointReference>")
 	 D SMSG.Insert("</wsp:AppliesTo>")
 	 D SMSG.Insert("<ns1:Issuer>")
 	 D SMSG.Insert("<wsa:Address>https://ssoi.domain.ext/Issuer/smtoken/SAML2</wsa:Address>")
 	 D SMSG.Insert("</ns1:Issuer>")
 	 D SMSG.Insert("<ns1:RequestType>http://schemas.xmlsoap.org/ws/2005/02/trust/Issue</ns1:RequestType>")
 	 D SMSG.Insert("<ns1:TokenType>http://docs.oasis-open.org/wss/oasis-wss-saml-token-profile-1.1#SAMLV2.0</ns1:TokenType>")
 
 	 Try
 	 {
 	  D CLIENT.RequestSecurityToken(SMSG) //send the soap request
 	 }
 	 Catch
 	 {
	  S %session.Data("WebError")="1^A "_$TR($ZERROR,"<>","[]")_" error has occurred while processing your request.<br>If you repeatedly see this message contact your help desk for assistance."
	  //S MESSAGE="1^A "_$TR($ZERROR,"<>","")_" error has occurred. <br>Please try again or contact your help desk for assistance."
  	 }
      
     S IO=""
     S U="^"
     S DUZ=0     
     S IO("IP")=$G(%request.CgiEnvs("REMOTE_ADDR"))           

     N TOKEN,LENGTH,I,BEG,END,ASOSKIP
         
     S TOKEN=CLIENT.HttpResponse.Data  //save the soap response
     
	 I TOKEN["<wst:Reason>" S %session.Data("WebError")="1^"_$P($P(TOKEN,"<wst:Reason>",2),"</wst:Reason>",1)_" <br>If you repeatedly see this message contact your help desk for assistance."
	 
     //save soap message into 200 character chunks
     S LENGTH=$L(TOKEN)
     S (END,I)=0
     K ^TMP("DGBT",$J)
     F I=1:1  Q:END>LENGTH  D
      .S BEG=END+1
      .S END=END+200
      .S ^TMP("DGBT",$J,I)=$E(TOKEN,BEG,END)
      .Q
         
     S ASOSKIP=1 D SETUP^XUSRB(,ASOSKIP) //setup signon variables - ASOSKIP=1, skip Auto Sign-On (recommended for 2FA)
     K RET D ESSO^XUESSO4(.RET,$NA(^TMP("DGBT",$J))) //pass in soap response from STS (SAML token)
     S DUZ=$G(RET(0))
     K ^TMP("DGBT",$J)
     S %session.Data("DUZ")=DUZ 
          
     I $G(%session.Data("DUZ"))>0 D
	 .I $$ACCESS^XQCHK($G(%session.Data("DUZ")),"DGBT BENE TRAVEL SCREEN")<1 D
	 ..S MESSAGE="1^You do not have access to this application"
	 ..K %session.Data("DUZ")
     	
     I $G(%session.Data("DUZ"))=0 D
     .K %session.Data("DUZ")
     .S %session.Data("AUTHFAIL")=1
     .D ##class(dgbt.Audit).LogAction(0)  
   }     
   	</script>         
   	<csp:if condition='$G(%session.Data("AUTHFAIL"))=1'>
  	 <script type="text/javascript">
  	 window.location.href = "dgbtLogin2.csp";
    </script> 
    </csp:if>
       
    <csp:if condition='$G(%session.Data("DUZ"),0)=0'>
    	<div class="center">
        <csp:if condition='$P($G(MESSAGE),"^",1)=0'>
        	<h1>#($P($G(MESSAGE),"^",2))#</h1>    
        <csp:else>
			<h1 class="error">#($P($G(MESSAGE),"^",2,$L(MESSAGE,"^")))#</h1>
        </csp:if>
        
        <table style="margin-left:auto; margin-right:auto;">
        	<tr>
            	<td style="font-size:small;">
              		<textarea readonly="readonly" cols="80" rows="23" style="background-color:#E8E8E8;">
                	<script language="cache" runat="server">
                	 D INTRO^XUSRB(.S1)
                 	 S IX=0 F  S IX=$O(S1(IX)) Q:'IX  D
                 	  .W !,..EscapeHTML($G(S1(IX)))
                   </script> 
              		</textarea>
            	</td>
          	</tr>
        </table>
        
        <form action="dgbtLogin.csp" method="post">
        	<table style="margin-left:auto; margin-right:auto;">
            	<tr>
              		<td></td>
                    <td><input type="submit" name="submit" value="Continue" /></td>
            	</tr>
          	</table>         
          	<div>
          		<input type="hidden" id="url" name="url" value="" />
            	<script type="text/javascript">
            	 document.getElementById('url').value='#($G(%request.Data("url",1)))#';
           	   </script>
          	</div>
        </form>
      	
      	</div>
	
    <csp:else>
    	<script language="cache" runat="server">
         S %session.Data("DT")=$$DT^XLFDT()
         S %session.Data("SITE")=$P($$SITE^VASITE($G(%session.Data("DT"))),"^",2) 
         I $G(%request.Data("url",1))="" S %request.Data("url",1)="dgbtDashboard.csp"
         S %session.AppTimeout=1200
         
         D ##class(dgbt.Audit).LogAction(1)
       </script>
 
        <script type="text/javascript">
         window.location.href = '#($G(%request.Data("url",1)))#'
       </script>
    </csp:if>
</body>
</html>]]></CSP>


<CSP name="dgbtLogin2.csp" application="/csp/bt/"><![CDATA[
<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01//EN" "http://www.w3.org/TR/html4/strict.dtd">
<html lang="en">

<head>
	<title>Beneficiary Travel Dashboard 1.0 p32</title>
	
    <style type="text/CSS">
    body { background-color:#e8e8e8; font-family:sans-serif; font-size:small; color:#00008b; }
	div.pageheader { font-weight:bold; position:fixed; top:0px; left:0px;  border-bottom:solid 1px black; background-color:#1e2d54;
  					width:98.5%; height:1.5em; color:White; font-size:12px;}
	div.pagebody { position:fixed; top:1.5em; left:0px; bottom:0px; width:99.9%; background-color:#e8e8e8; }
	div.center { margin-right:auto; margin-left:auto; text-align:center; }
	h1.error { color:red; }
   </style>
   
</head>
<body onLoad="document.getElementById('accesscode').focus();">
 
 <!--Legacy Access/Verify code functionality to be removed at a later date -->
 
    <csp:if condition='$G(%session.Data("AUTHFAIL"))<1'> 
    	<csp:if condition='$G(%request.Data("accesscode",1))=""'>
    	  	 <script type="text/javascript">
  	 		window.location.href = "dgbtLogin.csp";
    		</script> 
    	</csp:if>
    </csp:if>

	<script language="CACHE" runat="server">	
      
     N OSID S OSID=^%ZOSF("OS")
     I $SELECT(OSID["OpenM":$$OS^%ZOSV(),OSID["DSM":"VMS",1:"UNK")="VMS"
     {
      S MESSAGE="0^Please Login with your Vista Account"
     }
     Else
     { 
      S MESSAGE="1^Failed to validate your PIV credentials in VistA. Please log in with your VistA Access/Verify codes."
  	  I $G(%session.Data("WebError"))'=""
  	  {
	   S MESSAGE2=$G(%session.Data("WebError"))
  	  }
     }
     S IO=""
     S U="^"
     S DUZ=0     
     S IO("IP")=$G(%request.CgiEnvs("REMOTE_ADDR"))     
     S ACCESSCODE=$G(%request.Data("accesscode",1),"")
     S VERIFYCODE=$G(%request.Data("verifycode",1),"")     
     K %session.Data("AUTHFAIL")
     
     I (ACCESSCODE'="")!(VERIFYCODE'="") D
     .D SETUP^XUSRB()
     .K RET D VALIDAV^XUSRB(.RET,$$ENCRYP^XUSRB1(ACCESSCODE_";"_VERIFYCODE))
     .S DUZ=$G(RET(0))
     .S MESSAGE="1^"_$G(RET(3))
     .S %session.Data("DUZ")=DUZ 
          
     I $G(%session.Data("DUZ"))>0 D
	 .I $$ACCESS^XQCHK($G(%session.Data("DUZ")),"DGBT BENE TRAVEL SCREEN")<1 D
	 ..S MESSAGE="1^You do not have access to this application"
	 ..K %session.Data("DUZ")
     	
     I $G(%session.Data("DUZ"))=0 D
     .K %session.Data("DUZ")
     .D ##class(dgbt.Audit).LogAction(0)
   </script>
    
    <csp:if condition='$G(%session.Data("DUZ"),0)=0'>
    	<div class="center">
        <csp:if condition='$P($G(MESSAGE),"^",1)=0'>
        	<h1>#($P($G(MESSAGE),"^",2))#</h1>
        <csp:else>
          	<h1 class="error">#($P($G(MESSAGE),"^",2,$L(MESSAGE,"^")))#</h1>
          	<h6 style="color:black;">#($G(MESSAGE2))#</h6>
        </csp:if>
        
        <table style="margin-left:auto; margin-right:auto;">
        	<tr>
            	<td style="font-size:small;">
              		<textarea readonly="readonly" cols="80" rows="23" style="background-color:#E8E8E8;">
                	<script language="cache" runat="server">
                	 D INTRO^XUSRB(.S1)
                 	 S IX=0 F  S IX=$O(S1(IX)) Q:'IX  D
                 	 .W !,..EscapeHTML($G(S1(IX)))
               	   </script> 
              		</textarea>
            	</td>
          	</tr>
        </table>
        
        <form action="dgbtLogin2.csp" method="post">
        	<table style="margin-left:auto; margin-right:auto;">
            	<tr>
              		<td>VistA Access Code:</td>
              		<td align="left"><input type="password" id="accesscode" name="accesscode" /></td>
            	</tr>
            	<tr>
              		<td>VistA Verify Code:</td>
              		<td align="left"><input type="password" id="verifycode" name="verifycode" /></td>
            	</tr>
            	<tr>
              		<td></td>
              		<td><input type="submit" value="Submit" /></td>
            	</tr>
          	</table>
          
          	<div>
          		<input type="hidden" id="url" name="url" value="" />
            	<script type="text/javascript">
            	 document.getElementById('url').value='#($G(%request.Data("url",1)))#';
           	   </script>
          	</div>
        </form>
      	
      	</div>
      	
    <csp:else>
    	<script language="cache" runat="server">
         S %session.Data("DT")=$$DT^XLFDT()
         S %session.Data("SITE")=$P($$SITE^VASITE($G(%session.Data("DT"))),"^",2) 
         I $G(%request.Data("url",1))="" S %request.Data("url",1)="dgbtDashboard.csp"
         S %session.AppTimeout=1200
         
         D ##class(dgbt.Audit).LogAction(1)
       </script>
 
        <script type="text/javascript">
         window.location.href = '#($G(%request.Data("url",1)))#'
       </script>
    </csp:if>
</body>
</html>]]></CSP>


<CSP name="dgbtMenu.csp" application="/csp/bt/"><![CDATA[
<div id="menu" style="margin-bottom:10px;">
	<a href="dgbtDashboard.csp">Dashboard</a>
	|
	<!-- <a href="btConfiguration.csp">Configuration</a> | -->
	<a target="_blank" href="http://vaww1.domain/directory/guide/home.asp?isFlash=1">Facility Locator</a>
	|
	<a target="_BLANK" href="http://www.bing.com/maps/">Bing Maps</a>
	|
	<a href="dgbtClaims.csp">Claims</a>
	|
	<a href="dgbtPivotTable.csp">Pivot Table</a>
	|
	<a href="#" onclick="#server(..Logout())#">Logout</a>
</div>

<script language="cache" method="Logout">
K %session.Data("DUZ")

S CALLBACK=$G(%request.CgiEnvs("HTTP_REFERER"),"NOCALLBACK")
I CALLBACK["dgbtDashboard" S CALLBACK=$P(CALLBACK,"dgbtDashboard",1)_"dgbtLogin"_$P(CALLBACK,"dgbtDashboard",2)
S LINK=$G(%session.Data("LogoutURL"),"NOURL")

I LINK[".domain.ext/"
{
 S LOGOUT=$P(LINK,"/",1,4)_"/centrallanding.aspx?appid=BTD&target="_CALLBACK
 &js< window.location.href = '#(LOGOUT)#'; >
}
Else
{
 S LOGOUT="https://ssologon.domain.ext/centrallogin/centrallanding.aspx?appid=BTD&target="_CALLBACK
 &js< window.location.href = '#(LOGOUT)#'; >
}
</script>]]></CSP>


<CSP name="dgbtPivotTable.csp" application="/csp/bt/"><![CDATA[
<!DOCTYPE html "-//W3C//DTD XHTML 1.0 Strict//EN" 
  "http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd">
<html xmlns="http://www.w3.org/1999/xhtml">
<head>	
    <meta http-equiv="content-type" content="text/html; charset=utf-8"/>
    <title>Beneficiary Travel Dashboard 1.0 p32</title>
    
    <script src="jquery-ui-1.8.4.custom/js/jquery-1.4.2.min.js" type="text/javascript"></script>
    <script src="jquery-ui-1.8.4.custom/js/jquery-ui-1.8.4.custom.min.js" type="text/javascript"></script>
    <script src="DataTables-1.8.0/media/js/jquery.dataTables.js" language="JavaScript"></script>
	<script src="DataTables-1.8.0/extras/TableTools/media/js/ZeroClipboard.js"  type="text/javascript"></script>
    <script src="DataTables-1.8.0/extras/TableTools/media/js/TableTools.js"  type="text/javascript"></script>
    <script src="anytime.js"  type="text/javascript"></script>
    <script src="calendar.js"  type="text/javascript"></script>
    <script src="num-html.js" language="JavaScript"></script>
  	
	<link type="text/css" rel="stylesheet" href="bt.css" />
	<link href="jquery-ui-1.8.4.custom/css/smoothness/jquery-ui-1.8.4.custom.css" rel="Stylesheet" type="text/css" />
   	<link href="DataTables-1.8.0/media/css/demo_table.css" rel="stylesheet" type="text/css">	    
   	<link href="DataTables-1.8.0/extras/TableTools/media/css/TableTools.css" rel="Stylesheet" type="text/css" />
   	<link href="anytime.css" rel="Stylesheet" type="text/css" />
   	<link href="calendar.css" rel="Stylesheet" type="text/css" />
	 	   	
   	<script type="text/javascript">	   	   	
	$(document).ready(function() {	
		#server(dgbt.DashboardCb.OnLoad())#;
			
		/*
		var dates = $("#fromdate, #todate").datepicker({
            showButtonPanel: true,
            numberOfMonths: 1,
            changeYear: true,
            changeMonth: true,
            maxDate: '+0d',
            onSelect: function(selectedDate) {
                var option = this.id == "fromdate" ? "minDate" : "maxDate";
                var instance = $(this).data("datepicker");
                var date = $.datepicker.parseDate(instance.settings.dateFormat || $.datepicker._defaults.dateFormat, selectedDate, instance.settings);
                dates.not(this).datepicker("option", option, date);
            }
        });
        */
        
        //$("#fromdate").AnyTime_picker({format:"%c/%e/%Y"});
        //$("#todate").AnyTime_picker({format:"%c/%e/%Y"});
                 
		oTable = $('#myTable').dataTable({
			"bFilter": true,
			"bPaginate": true,
			"iDisplayLength": 50,
			"sPaginationType": "two_button",
			"bProcessing": true,
			"bAutoWidth": false,
			"oTableTools": { "sSwfPath": "DataTables-1.8.0/extras/TableTools/media/swf/copy_cvs_xls.swf", "aButtons": ["xls"] },
			"aoColumns": [
				{ "sTitle": "<a style=\"color:black;\" href=\"#\">Patient</a>" },
				{ "sTitle": "<a style=\"color:black;\" href=\"#\">Claim Date/Time</a>", "sType":"date" },
				{ "sTitle": "<a style=\"color:black;\" href=\"#\">Account</a>" },
				{ "sTitle": "<a style=\"color:black;\" href=\"#\">Amount Payable</a>", "sType":"num-html" },
				{ "sTitle": "<a style=\"color:black;\" href=\"#\">Who Entered</a>" },
				{ "sTitle": "<a style=\"color:black;\" href=\"#\">Payee</a>" },
				{ "sTitle": "<a style=\"color:black;\" href=\"#\">Place of Departure</a>" },
				{ "sTitle": "<a style=\"color:black;\" href=\"#\">City of Departure</a>" },
				{ "sTitle": "<a style=\"color:black;\" href=\"#\">Zip Code</a>" }
	   		],
	   		"sDom": 'T<"clear">lpfrtip'
		});
		
		// hacks for 508 compliance
		//$('.dataTables_filter').html("<label>Search:<input type=\"text\"></label>");
		$('.dataTables_filter').prepend("<label>");
		$('.dataTables_filter').append("</label>");		
		$('#myTable_previous').wrap('<a href="#" title="previous" />');
		$('#myTable_next').wrap('<a href="#" title="next" />');
	 	//$('.DTTT_container span').wrapInner('<a href="#" />');
	 	$('#ZeroClipboardMovie_1').attr("tabindex","-1");
	 	$('#ZeroClipboardMovie_2').attr("tabindex","-1");
	 	$('#ToolTables_myTable_0').attr("tabindex","-1");
	 	$('#ToolTables_myTable_1').attr("tabindex","-1");
	 						
		RefreshView();
	});
	
	function RefreshView() {
		$("#ajaxloader").empty().html('<img src="ajax-loader.gif" />');
		#call(..DisplayClaims($("#fromdate").val(),$("#todate").val()))#;
	}
	
	</script>
</head>
<body>
	<csp:include page="dgbtMenu.csp" />
	
	<div>	
		<label for="fromdate">From</label><input type="text" id="fromdate" value="#($$FMTE^XLFDT($$FMADD^XLFDT($$DT^XLFDT,-1,0,0,0),"5D"))#" />
        <label for="todate">To</label><input type="text" id="todate" value="#($$FMTE^XLFDT($$FMADD^XLFDT($$DT^XLFDT,-1,0,0,0),"5D"))#" />(format: xx/xx/xxxx)
        <input type="button" value="Go" onclick="RefreshView()" />
        <br />
        <em>only the first 3000 records will display</em>
	</div>
	
	<div id="ajaxloader" style="margin-top:10px; margin-left:48%;"></div>
	
	<div>
		<table style="margin-bottom:10px;">
			<tr>
				<td>Amount Payable:</td>
				<td><input id="amountpayable" readonly="true" /></td>
			</tr>
		</table>
	</div>	
	
	<div>
		<table id="myTable" class="display"></table>
	</div>
	  	
	<script language="cache" method="DisplayClaims" arguments="START,END">
	D ##class(dgbt.Audit).LogAction(2,START_"^"_END)
	
	&js< var array1 = []; >
	
	S X=END,%DT("X")=END D ^%DT S END=Y_.999999
	S X=START,%DT("X")=START D ^%DT S START=Y	
	Q:END<=START
	
	S TOTAL=0
	S COUNT=0
	S MAX=3000
 	S ENDDATE=$$DT^XLFDT_".999999"
 	S LOOPDT=START 
 	F  S LOOPDT=$O(^DGBT(392,"B",LOOPDT)) Q:((LOOPDT<1)!(LOOPDT>END)!(COUNT>=MAX))  D
 	.S CLAIMIEN="" F  S CLAIMIEN=$O(^DGBT(392,"B",LOOPDT,CLAIMIEN)) Q:(CLAIMIEN="")  D
 	..K ARRAY D GETS^DIQ(392,CLAIMIEN,".01;2;10;12;6;42;21;24;24.2","EI","ARRAY","")
 	..S AMOUNTPAYABLE=$G(ARRAY(392,CLAIMIEN_",",10,"E"))
 	..S PATIENT=$G(ARRAY(392,CLAIMIEN_",",2,"E"))
 	..S AJRDFN=$G(ARRAY(392,CLAIMIEN_",",2,"I"))
 	..S LAST4=$E($$GET1^DIQ(2,AJRDFN,.09),6,9)
 	..S CLAIMDATE=$G(ARRAY(392,CLAIMIEN_",",.01,"I")),CLAIMDATE=$$FMTE^XLFDT(CLAIMDATE,"5"),CLAIMDATE=$TR(CLAIMDATE,"@"," ")
 	..S WHOENTERED=$G(ARRAY(392,CLAIMIEN_",",12,"E"))
 	..S ACCOUNT=$G(ARRAY(392,CLAIMIEN_",",6,"E"))
 	..S PAYEE=$G(ARRAY(392,CLAIMIEN_",",42,"E"))
 	..S PLACE=$G(ARRAY(392,CLAIMIEN_",",21,"E"))
 	..S CITY=$G(ARRAY(392,CLAIMIEN_",",24,"E"))
 	..S ZIPCODE=$G(ARRAY(392,CLAIMIEN_",",24.2,"E"))
	..&js< array1.push([
  			"#(PATIENT)# (#(LAST4)#)",
  			"#(CLAIMDATE)#",
  			"#(ACCOUNT)#",
  			"#(AMOUNTPAYABLE)#",
  			"#(WHOENTERED)#",
  			"#(PAYEE)#",
  			"#(PLACE)#",
  			"#(CITY)#",
  			"#(ZIPCODE)#"
  			]);
  		>
  	..S TOTAL=TOTAL+AMOUNTPAYABLE
  	..S COUNT=COUNT+1
  		
  	&js<
  		oTable.fnClearTable(0);
	 	if (array1.length != 0) {
			oTable.fnAddData(array1);
	 	}
	 	$("#amountpayable").val("#(TOTAL)#");
	 	
	 	$("#ajaxloader").empty();
 		>
	</script>
</body>
</html>]]></CSP>


<Class name="dgbt.Audit">
<ClassType/>
<Language>cache</Language>
<ProcedureBlock>0</ProcedureBlock>
<Super>%CSP.Page</Super>
<TimeChanged>62363,31384.255687</TimeChanged>
<TimeCreated>62363,31384.255687</TimeCreated>

<Method name="OnPage">
<ClassMethod>1</ClassMethod>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	&html<<html>
<head>
</head>
<body>>
	; To do...
	&html<</body>
</html>>
	Quit $$$OK
]]></Implementation>
</Method>

<Method name="LogAction">
<ClassMethod>1</ClassMethod>
<FormalSpec>AJRACTION,ARGS</FormalSpec>
<Implementation><![CDATA[
 S ACTION=""
 I AJRACTION=0 S ACTION="Failed login from "_$G(%request.CgiEnvs("REMOTE_ADDR"))
 E  I AJRACTION=1 S ACTION="Successful login"
 E  I AJRACTION=2 S ACTION="PivotTable "_$P($G(ARGS),"^",1)_" to "_$P($G(ARGS),"^",2)
 E  I AJRACTION=3 S ACTION="Claims screen"
 
 S AJRDUZ=$G(%session.Data("DUZ"))
 I AJRDUZ<1 S AJRDUZ=""
 
 K FDA 
 S FDA(392.51,"+1,",.01)=$$NOW^XLFDT
 S FDA(392.51,"+1,",1)=AJRDUZ
 S FDA(392.51,"+1,",2)=ACTION
 D UPDATE^DIE("","FDA","RESULTS","")
]]></Implementation>
</Method>
</Class>


<Class name="dgbt.DashboardCb">
<Super>%CSP.Page</Super>
<TimeChanged>64343,76280.87964</TimeChanged>
<TimeCreated>62363,31515.535134</TimeCreated>

<Method name="OnPage">
<ClassMethod>1</ClassMethod>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[ Quit $$$OK
]]></Implementation>
</Method>

<Method name="OnLoad">
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[
 S EXPIRES=$$HADD^XLFDT($H,0,0,20,0)
 S EXPIRES=$ZD(EXPIRES,11)_", "_$ZD(EXPIRES,2)_" "_$ZT($P(EXPIRES,",",2),1)_" GMT"
 S AJRDUZ=%request.GetCookie("DUZ") 
 I $G(AJRDUZ)>0 S %session.Data("DUZ")=AJRDUZ
 I $G(%session.Data("DUZ"),0)<1 &js< window.location.href = "dgbtLogin.csp?url=dgbtDashboard.csp"; >
 E  I AJRDUZ="" D %response.SetCookie("DUZ",$G(%session.Data("DUZ"),0),EXPIRES,"/csp/reports")
]]></Implementation>
</Method>

<Method name="CalcRoute">
<ClassMethod>1</ClassMethod>
<FormalSpec>AJRDFN:%String,PATIENTADDRESS:%String</FormalSpec>
<Implementation><![CDATA[
  	
 S FILE2("ZIP CODE")="-",STATE=""
 I PATIENTADDRESS="" D
 .S PATIENTADDRESS=##class(dgbt.Patient).GetAddress(AJRDFN)
 .S FILE2("ZIP CODE")=##class(dgbt.Patient).GetZipcode(AJRDFN)

 Q:((PATIENTADDRESS="")!(FILE2("ZIP CODE")=""))
 &js< 	  	
  	patientAddress = "#(PATIENTADDRESS)#";
  	patientZip = "#(FILE2("ZIP CODE"))#";
	CalculateMileage();		
  	>
]]></Implementation>
</Method>

<Method name="UpdateDisplay">
<ClassMethod>1</ClassMethod>
<FormalSpec>AJRDFN:%String</FormalSpec>
<Implementation><![CDATA[
 Q:$G(AJRDFN)=""
 &js< 
 	$('#ajrdfn').val('#(AJRDFN)#');
 	oTable.fnClearTable(0);
 	>
 D ..UpdateDetails(AJRDFN)
 D ..CalcRoute(AJRDFN,"")
]]></Implementation>
</Method>

<Method name="UpdateDetails">
<ClassMethod>1</ClassMethod>
<FormalSpec>AJRDFN:%String</FormalSpec>
<Implementation><![CDATA[
 S PATIENTDETAILS=##class(dgbt.Patient).GetDetails(AJRDFN)
 S APPOINTMENTS=##class(dgbt.Patient).GetAppointments(AJRDFN)
 S NOTES=##class(dgbt.Patient).GetNotes(AJRDFN)
 S ORDERS=##class(dgbt.Patient).GetOrders(AJRDFN)
 S CONSULTS=##class(dgbt.Patient).GetConsults(AJRDFN)
 S CLAIMS=##class(dgbt.Patient).GetClaims(AJRDFN)
 
 &js<
	var html = '#(PATIENTDETAILS)#' + '#(APPOINTMENTS)#' + '#(CLAIMS)#' + '#(NOTES)#' + '#(ORDERS)#' + '#(CONSULTS)#';
 	
 	$("#content").html(html);
	$(".oldappt").hide();
	html = null;
	>
]]></Implementation>
</Method>

<Method name="InitializeClaimers">
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[
 S TIMESTAMP=$G(^XTMP("DGBT BTD","TIMESTAMP"))
 I TIMESTAMP'=$$DT^XLFDT K ^XTMP("DGBT BTD")
 S ^XTMP("DGBT BTD","TIMESTAMP")=$$DT^XLFDT
 
 &js< document.getElementById('claimer').options.add(new Option('Select...','')); >
 S AJRDUZ="" F  S AJRDUZ=$O(^XTMP("DGBT BTD","CLAIMERS",AJRDUZ)) Q:AJRDUZ=""  D
 .I AJRDUZ>0 D
 ..&js<
 	var option = new Option("#($$GET1^DIQ(200,AJRDUZ,.01))#",'#(AJRDUZ)#');
 	document.getElementById('claimer').options.add(option);	>
]]></Implementation>
</Method>

<Method name="EncodeJs">
<ClassMethod>1</ClassMethod>
<FormalSpec>JSSTRING:%String</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
 S SPEC("'")="\'"
 ;S SPEC("\\'")="\'"
 ;S SPEC("""")="\"""
 S SPEC(";")=","
 S JSSTRING=$$REPLACE^XLFSTR(JSSTRING,.SPEC)
 Q JSSTRING
]]></Implementation>
</Method>
</Class>


<Class name="dgbt.Institution">
<ClassType/>
<Language>cache</Language>
<ProcedureBlock>0</ProcedureBlock>
<Super>%CSP.Page</Super>
<TimeChanged>62363,31479.632742</TimeChanged>
<TimeCreated>62363,31479.632742</TimeCreated>

<Method name="InitializeInstitutions">
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[
 S CONFIGIEN=1

 S INSTITUTIONIEN="" F  S INSTITUTIONIEN=$O(^DGBT(392.5,CONFIGIEN,1,"B",INSTITUTIONIEN)) Q:INSTITUTIONIEN<1  D
 .S IEN="" F  S IEN=$O(^DGBT(392.5,CONFIGIEN,1,"B",INSTITUTIONIEN,IEN)) Q:IEN<1  D
 ..S ACTIVE=$P($G(^DGBT(392.5,CONFIGIEN,1,IEN,0)),"^",2)
 ..Q:$G(ACTIVE)'=1
 ..;
 ..S FILE4("STATION NUMBER")=$$GET1^DIQ(4,INSTITUTIONIEN,99)
 ..S FILE4("NAME")=$$GET1^DIQ(4,INSTITUTIONIEN,.01)
 ..S ADDRESS=..GetAddress(IEN,INSTITUTIONIEN)
 ..S SPECIALTYLIST=..GetSpecialtyList(1,IEN)
 ..;
 ..I ((FILE4("STATION NUMBER")'="")&(ADDRESS'="")&(FILE4("NAME")'="")) D
 ...S ARRAYITEM=FILE4("STATION NUMBER")_"^"_FILE4("NAME")_"^"_ADDRESS_"^"_SPECIALTYLIST
 ...&js< 
 	var site = new Site();
	site.StationNumber = "#(FILE4("STATION NUMBER"))#";
	site.Name = "#(FILE4("NAME"))#";
	site.Address = "#(ADDRESS)#";
	site.SpecialtyList = "#(SPECIALTYLIST)#";

	siteArray.push(site);  
	site = null; >

 ; Institutions not in the Institution file.
 S INSTITUTIONNAME="" F  S INSTITUTIONNAME=$O(^DGBT(392.5,CONFIGIEN,2,"B",INSTITUTIONNAME)) Q:INSTITUTIONNAME=""  D
 .S IEN="" F  S IEN=$O(^DGBT(392.5,CONFIGIEN,2,"B",INSTITUTIONNAME,IEN)) Q:IEN<1  D
 ..S ACTIVE=$P($G(^DGBT(392.5,CONFIGIEN,2,IEN,0)),"^",2)
 ..Q:$G(ACTIVE)'=1
 ..;
 ..S TEMP("STATION NUMBER")="00BT"_IEN
 ..S TEMP("NAME")=INSTITUTIONNAME
 ..S TEMP("ADDRESS")=$P($G(^DGBT(392.5,CONFIGIEN,2,IEN,0)),"^",3)
 ..S TEMP("SPECIALTYLIST")=..GetSpecialtyList(2,IEN)
 ..;
 ..I ((TEMP("STATION NUMBER")'="")&(TEMP("ADDRESS")'="")&(TEMP("NAME")'="")) D
 ...S ARRAYITEM=TEMP("STATION NUMBER")_"^"_TEMP("NAME")_"^"_TEMP("ADDRESS")_"^"_TEMP("SPECIALTYLIST")
 ...&js< 
 	var site = new Site();
	site.StationNumber = "#(TEMP("STATION NUMBER"))#";
	site.Name = "#(TEMP("NAME"))#";
	site.Address = "#(TEMP("ADDRESS"))#";
	site.SpecialtyList = "#(TEMP("SPECIALTYLIST"))#";
	siteArray.push(site);
	site = null; >
]]></Implementation>
</Method>

<Method name="GetAddress">
<ClassMethod>1</ClassMethod>
<FormalSpec>IEN:%String,INSTITUTIONIEN:%String</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
 S CONFIGIEN=1
 ;
 S ADDRESS=""
 S ADDRESS=$P($G(^DGBT(392.5,CONFIGIEN,1,IEN,0)),"^",3)
 I ADDRESS="" D
 .S FILE4("STREET ADDR 1")=$$GET1^DIQ(4,INSTITUTIONIEN,1.01)
 .S FILE4("STREET ADDR 2")=$$GET1^DIQ(4,INSTITUTIONIEN,1.02)
 .S FILE4("CITY")=$$GET1^DIQ(4,INSTITUTIONIEN,1.03)
 .S FILE4("STATE")=$$GET1^DIQ(4,INSTITUTIONIEN,.02)
 .S FILE4("ZIP")=$$GET1^DIQ(4,INSTITUTIONIEN,1.04)
 .S ADDRESS=FILE4("STREET ADDR 1")_" "_FILE4("STREET ADDR 2")_", "_FILE4("CITY")_", "_FILE4("STATE")_" "_FILE4("ZIP")
 Q ADDRESS
]]></Implementation>
</Method>

<Method name="GetSpecialtyList">
<ClassMethod>1</ClassMethod>
<FormalSpec>NODE:%String,IEN:%String</FormalSpec>
<Implementation><![CDATA[
 N CONFIGIEN,SPECIALTYLIST,SPECIALTY
 S CONFIGIEN=1
 S SPECIALTYLIST=""
 S SPECIALTY="" 
 ;
 F  S SPECIALTY=$O(^DGBT(392.5,CONFIGIEN,NODE,IEN,1,"B",SPECIALTY)) Q:SPECIALTY=""  D
 .S SPECIALTYLIST=SPECIALTYLIST_SPECIALTY_"<br />"
 Q SPECIALTYLIST
]]></Implementation>
</Method>
</Class>


<Class name="dgbt.Patient">
<Super>%CSP.Page</Super>
<TimeChanged>64055,41646.264254</TimeChanged>
<TimeCreated>62363,31454.198949</TimeCreated>

<Method name="GetAlert">
<ClassMethod>1</ClassMethod>
<FormalSpec>AJRDFN:%String</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
 S ALERT=""
 ;I $D(^ZTRAV(AJRDFN)) S ALERT=$P(^ZTRAV(AJRDFN),"^",3)
 Q ALERT
]]></Implementation>
</Method>

<Method name="GetAddress">
<ClassMethod>1</ClassMethod>
<FormalSpec>AJRDFN:%String</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
 S PATIENTADDRESS=""
 S FILE2("STREET ADDRESS [LINE 1]")=$$GET1^DIQ(2,AJRDFN,.111)
 S FILE2("STREET ADDRESS [LINE 2]")=$$GET1^DIQ(2,AJRDFN,.112)
 S FILE2("STREET ADDRESS [LINE 3]")=$$GET1^DIQ(2,AJRDFN,.113)
 S FILE2("CITY")=$$GET1^DIQ(2,AJRDFN,.114)
 S FILE2("STATE")=$$GET1^DIQ(2,AJRDFN,.115)
 S FILE2("ZIP CODE")=$$GET1^DIQ(2,AJRDFN,.116)
 S PATIENTADDRESS=FILE2("STREET ADDRESS [LINE 1]")_" "_FILE2("STREET ADDRESS [LINE 2]")_" "_FILE2("STREET ADDRESS [LINE 3]")
 S PATIENTADDRESS=PATIENTADDRESS_", "_FILE2("CITY")_", "_FILE2("STATE")_" "_FILE2("ZIP CODE")
 Q PATIENTADDRESS
]]></Implementation>
</Method>

<Method name="GetDetails">
<ClassMethod>1</ClassMethod>
<FormalSpec>AJRDFN:%String</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
 S ALERT=..GetAlert(AJRDFN)
 S PATIENTADDRESS=..GetAddress(AJRDFN)
 
 S HTML="<table><thead><tr><th>Patient Name</th><th>Last 4</th><th>SC %</th></tr></thead><tbody>"
 S HTML=HTML_"<tr><td>"_$$GET1^DIQ(2,AJRDFN,.01)_"</td><td>"_$E($$GET1^DIQ(2,AJRDFN,.09),6,9)_"</td><td>"_$$GET1^DIQ(2,AJRDFN,.302)_"</td></tr>"
 S HTML=HTML_"<tr><td colspan=""3"" id=""patientaddress"">"_PATIENTADDRESS_"</td></tr>"
 S HTML=HTML_"<tr><td colspan=""3""><label for=""paddress"">Alternate Address:</label><input type=""text"" name=""paddress"" id=""paddress"" size=""40"" />"
 S HTML=HTML_"<input type=""button"" value=""Use Alternate"" onclick=""UseAlternate()"" /></td></tr>"
 I ALERT'="" S HTML=HTML_"<tr><td colspan=""3"">Travel Alert: "_ALERT_"</td></tr>"
  
 S HTML=HTML_"</tbody></table>"
 Q ..EncodeJs(HTML)
]]></Implementation>
</Method>

<Method name="GetZipcode">
<ClassMethod>1</ClassMethod>
<FormalSpec>AJRDFN:%String</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
 S FILE2("ZIP CODE")=""
 S FILE2("ZIP CODE")=$$GET1^DIQ(2,AJRDFN,.116)
 Q FILE2("ZIP CODE")
]]></Implementation>
</Method>

<Method name="GetStateAbbr">
<ClassMethod>1</ClassMethod>
<FormalSpec>AJRDFN:%String</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
 S STATE=$$GET1^DIQ(2,AJRDFN,.115,"I")
 S STATE=$$GET1^DIQ(5,STATE,1) 
 Q STATE
]]></Implementation>
</Method>

<Method name="GetAppointments">
<ClassMethod>1</ClassMethod>
<FormalSpec>AJRDFN:%String</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
 S U="^" 
 D DUZ^XUP($G(%session.Data("DUZ"),1))
 ;S DUZ(2)=$O(^VA(200,$G(%session.Data("DUZ"),.5),2,0))
 ;&js< alert("#(DUZ(2))#"); >
 ;q ""
 
 S HTML="<table style=""margin-top:20px""><thead><tr><th>Appointment Location <input type=""button"" value=""Toggle"" onclick=""$(\'.oldappt\').toggle();"" /></th><th>Date/Time</th><th>Status</th></tr></thead></tbody>"
  
 S COUNT=0 
 S TEMPRESULTS=""
 S BEG=$$FMADD^XLFDT($$DT^XLFDT,-61,0,0,0)
 S END=$$FMADD^XLFDT($$DT^XLFDT,1,0,0,0)
 D VST^ORWCV(.TEMPRESULTS,AJRDFN,BEG,END,,)
 S IX=9999999999 F  S IX=$O(TEMPRESULTS(IX),-1) Q:IX=""!(COUNT>=25)  D
 .S FLAG=0,CLINICIEN="",STOPCODEIEN="",STOPCODE="",STYLE=""
 .S APPTDT=$P($G(TEMPRESULTS(IX)),"^",2)
 .S CLINIC=$P($G(TEMPRESULTS(IX)),"^",3),CLINIC=..EncodeJs(CLINIC)
 .S STYLE="" I APPTDT>$$NOW^XLFDT S STYLE="style=""background-color:red;"""
 .S STATUS=$P($G(TEMPRESULTS(IX)),"^",4)
 .;
 .I 1=1 D ;STATUS="" D
 ..S STATUS2=""
 ..S HOSLOC="",HOSLOC=$O(^SC("B",$P($G(TEMPRESULTS(IX)),"^",3),HOSLOC))
 ..I HOSLOC>0 D
 ...S STATUS2=$$STATUS^SDAM1(AJRDFN,APPTDT,HOSLOC,$G(^DPT(AJRDFN,"S",APPTDT,0)))
 ...S STATUS2=$P(STATUS2,";",3)
 ...I STATUS2'="" S STATUS=STATUS_"^"_STATUS2_"*"
 .;
 .;
 .I $E(APPTDT,1,7)=$$DT^XLFDT S HTML=HTML_"<tr "_STYLE_"><td>"_CLINIC_"</td><td>"_$$FMTE^XLFDT(APPTDT,"5")_"</td><td>"_STATUS_"</td></tr>"
 .E  S HTML=HTML_"<tr class=""oldappt"" "_STYLE_"><td>"_CLINIC_"</td><td>"_$$FMTE^XLFDT(APPTDT,"5")_"</td><td>"_STATUS_"</td></tr>"
 .S COUNT=COUNT+1
 
 S HTML=HTML_"</tbody></table>"
 Q HTML
]]></Implementation>
</Method>

<Method name="GetNotes">
<ClassMethod>1</ClassMethod>
<FormalSpec>AJRDFN:%String</FormalSpec>
<Implementation><![CDATA[
 S U="^" 
 ;D DUZ^XUP($G(%session.Data("DUZ"),1))
 
 S HTML="<table style=""margin-top:20px""><thead><tr><th>Note Location</th><th>Date/Time</th><th>Note Title</th></tr></thead><tbody>"
 
 S COUNT=0
 S TEMPRESULTS=""
 S BEG=$$FMADD^XLFDT($$DT^XLFDT,-1,0,0,0)
 S END=$$FMADD^XLFDT($$DT^XLFDT,1,0,0,0)
 D CONTEXT^TIUSRVLO(.TEMPRESULTS,3,1,AJRDFN,BEG,END,0,15,"D",1)
 S IX="" F  S IX=$O(^TMP("TIUR",$J,IX)) Q:IX=""!(COUNT>=15)  D
 .S TITLE=$P($G(^TMP("TIUR",$J,IX)),"^",2)
 .S LOCATION=$P($G(^TMP("TIUR",$J,IX)),"^",6)
 .S NOTEDATE=$P($G(^TMP("TIUR",$J,IX)),"^",3)
 .I $E(NOTEDATE,1,7)=$$DT^XLFDT S HTML=HTML_"<tr><td>"_LOCATION_"</td><td>"_$$FMTE^XLFDT(NOTEDATE,"5")_"</td><td>"_TITLE_"</td></tr>"
 .S COUNT=COUNT+1
 
 S HTML=HTML_"</tbody></table>"
 Q ..EncodeJs(HTML)
]]></Implementation>
</Method>

<Method name="GetOrders">
<ClassMethod>1</ClassMethod>
<FormalSpec>AJRDFN:%String</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
 S U="^" 
 ;D DUZ^XUP($G(%session.Data("DUZ"),1))
 
 S HTML="<table style=""margin-top:20px""><thead><tr><th>Order Location</th><th>Activity Date</th><th>Orderable Item</th><th>Status</th></tr></thead><tbody>"

 K IFNLIST,TEMPRESULTS,^TMP("ORR",$J)
 S COUNT=0
 S TEMPRESULTS=""
 S BEG=$$FMADD^XLFDT($$DT^XLFDT,-100,0,0,0)
 S END=$$FMADD^XLFDT($$DT^XLFDT,1,0,0,0)
 D AGET^ORWORR(.TEMPRESULTS,AJRDFN,"2^0",1,BEG,END,,0)
 S HIX="" F  S HIX=$O(^TMP("ORR",$J,HIX)) Q:HIX=""  D
 .S IX=.2 F  S IX=$O(^TMP("ORR",$J,HIX,IX)) Q:IX<1!(COUNT>=15)  D
 ..S ACTIVITYDATE=$P($G(^TMP("ORR",$J,HIX,IX)),"^",3)
 ..I $E(ACTIVITYDATE,1,7)=$$DT^XLFDT D
 ...S ORDERIEN=$P($G(^TMP("ORR",$J,HIX,IX)),"^",1)
 ...S ORDERIEN=$P(ORDERIEN,";",1)
 ...;
 ...S ITEMLIST=""
 ...S ITEMIEN="" F  S ITEMIEN=$O(^OR(100,ORDERIEN,.1,"B",ITEMIEN)) Q:ITEMIEN=""  D
 ....S ITEM=$$GET1^DIQ(101.43,ITEMIEN,.01)
 ....S ITEMLIST=ITEMLIST_ITEM_", "
 ...S HTML=HTML_"<tr><td>"_$$GET1^DIQ(100,ORDERIEN,6)_"</td><td>"_$$FMTE^XLFDT(ACTIVITYDATE,"5")_"</td><td>"_ITEMLIST_"</td><td>"_$$GET1^DIQ(100,ORDERIEN,5)_"</td></tr>"
 ...S COUNT=COUNT+1

 S HTML=HTML_"</tbody></table>"
 Q ..EncodeJs(HTML)
]]></Implementation>
</Method>

<Method name="GetConsults">
<ClassMethod>1</ClassMethod>
<FormalSpec>AJRDFN:%String</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
 S U="^" 
 ;D DUZ^XUP($G(%session.Data("DUZ"),1))
 
 S HTML="<table style=""margin-top:20px""><thead><tr><th>Consult Location</th><th>Date Of Request</th></tr></thead><tbody>"
 
 S COUNT=0
 S TEMPRESULTS=""
 S BEG=$$FMADD^XLFDT($$DT^XLFDT,-40,0,0,0)
 S END=$$FMADD^XLFDT($$DT^XLFDT,1,0,0,0)
 D LIST^ORQQCN(.TEMPRESULTS,AJRDFN,,,,)
 S IX="" F  S IX=$O(^TMP("ORQQCN",$J,"CS",IX)) Q:IX=""!(COUNT>=15)  D
 .S DATETIME=$P($G(^TMP("ORQQCN",$J,"CS",IX,0)),"^",2)
 .I DATETIME>$$FMADD^XLFDT($$DT^XLFDT,-730,0,0,0) D
 ..S TOSERVICE=$P($G(^TMP("ORQQCN",$J,"CS",IX,0)),"^",4)
 ..;I ((TOSERVICE["FEE")&(DATETIME>BEG)) D
 ..S HTML=HTML_"<tr><td>"_TOSERVICE_"</td><td>"_$$FMTE^XLFDT(DATETIME,"5")_"</td></tr>"
 ..S COUNT=COUNT+1
  
 S HTML=HTML_"</tbody></table>"
 Q ..EncodeJs(HTML)
]]></Implementation>
</Method>

<Method name="GetClaims">
<ClassMethod>1</ClassMethod>
<FormalSpec>AJRDFN:%String</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
 S HTML="<table style=""margin-top:20px""><thead><tr><th>Claim One Way</th><th>Date/Time</th><th>Remarks</th><th>Deductible</th></tr></thead><tbody>"

 S COUNT=0
 S FLAG=0
 S CLAIMDATE=$$DT^XLFDT
 S ENDDATE=$$FMADD^XLFDT(CLAIMDATE,-35,0,0,0)
 S CLAIMIEN=9999999999 F  S CLAIMIEN=$O(^DGBT(392,"C",AJRDFN,CLAIMIEN),-1) Q:(CLAIMIEN<1)!(CLAIMDATE<ENDDATE)!(COUNT>=10)  D
 .S CLAIMDATE=$$GET1^DIQ(392,CLAIMIEN,.01)
 .S REMARKS=$$GET1^DIQ(392,CLAIMIEN,51)
 .S ONEWAY=$$GET1^DIQ(392,CLAIMIEN,32) I ONEWAY="" S REMARKS="Claim in progress"
 .S DEDUCTIBLE=$$GET1^DIQ(392,CLAIMIEN,9)
 .S HTML=HTML_"<tr><td>"_ONEWAY_"</td><td>"_CLAIMDATE_"</td><td>"_REMARKS_"</td><td>"_DEDUCTIBLE_"</td></tr>"
 .S CLAIMDATE=$$GET1^DIQ(392,CLAIMIEN,.01,"I")
 .S COUNT=COUNT+1
    
 S HTML=HTML_"</tbody></table>"
 Q ..EncodeJs(HTML)
]]></Implementation>
</Method>

<Method name="EncodeJs">
<ClassMethod>1</ClassMethod>
<FormalSpec>JSSTRING:%String</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
 S SPEC("'")="\'"
 ;S SPEC("\\'")="\'"
 ;S SPEC("""")="\"""
 S SPEC(";")=","
 S JSSTRING=$$REPLACE^XLFSTR(JSSTRING,.SPEC)
 Q JSSTRING
]]></Implementation>
</Method>
</Class>
</Export>
